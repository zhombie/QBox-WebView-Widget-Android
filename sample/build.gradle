plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def properties = new Properties()
properties.load(new FileInputStream(rootProject.file('local.properties')))

def callTopic = properties.getProperty('call.topic') ?: '"general"'
def callRoutes = properties.getProperty('call.routes') ?: '"localhost*http://127.0.0.1"'

android {
    namespace 'kz.qbox.widget.webview.sample'

    compileSdk 33

    defaultConfig {
        applicationId 'kz.qbox.widget.webview'

        minSdk 21
        targetSdk 33

        versionCode project.ext.version_code
        versionName version
    }

    buildTypes {
        debug {
            buildConfigField 'String', 'CALL_TOPIC', callTopic
            buildConfigField 'String', 'CALL_ROUTES', callRoutes

            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Core
    implementation 'androidx.core:core-ktx:1.9.0'

    // Material Design Components
    implementation 'com.google.android.material:material:1.8.0'

    // Garage SDK
    implementation 'com.github.zhombie.garage:image-coil:1.3.5'

    implementation project(':core')
}